plugins {
    id 'org.springframework.boot' version '3.0.0'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'java'
    id 'jacoco'
    id 'maven-publish'
    id 'com.google.osdetector' version '1.7.1'
    id 'org.springdoc.openapi-gradle-plugin' version '1.6.0'
    id 'org.graalvm.buildtools.native' version '0.9.18'
    id 'nu.studer.jooq' version '8.0'
}

sourceCompatibility = 17
targetCompatibility = 17

group project.maven_group
version project.artifact_version

repositories {
    maven { url 'https://repo.spring.io/release' }
    mavenCentral()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    // spring configuration
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    // webflux
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    // r2dbc
    implementation 'org.postgresql:r2dbc-postgresql:1.0.0.RELEASE'
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'

    // jooq
    implementation 'org.jooq:jooq:3.17.6'
    jooqGenerator 'org.jooq:jooq-meta-extensions:3.17.6'
    jooqGenerator 'org.postgresql:postgresql:42.5.1'

    // observability from reference: https://spring.io/blog/2022/10/12/observability-with-spring-boot-3
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webflux-ui:2.0.0'
    implementation 'org.springdoc:springdoc-openapi-starter-webflux-api:2.0.0'

    // validation
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'io.github.wimdeblauwe:error-handling-spring-boot-starter:4.0.0'

    // datatype
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    // lombok
    implementation 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // testing
    implementation platform('org.testcontainers:testcontainers-bom:1.17.6')
    testImplementation(
            "org.junit.jupiter:junit-jupiter-api:5.9.0",
            'org.springframework.boot:spring-boot-starter-test',
            'org.mockito:mockito-inline:4.8.0',
            'io.projectreactor:reactor-test',
            'org.testcontainers:junit-jupiter',
            'org.testcontainers:postgresql',
            'org.testcontainers:r2dbc',
    )
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.9.0"
    testAnnotationProcessor 'org.projectlombok:lombok'
}

test {
    useJUnitPlatform()
}

check {
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    finalizedBy jacocoTestCoverageVerification
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

/* JACOCO */
jacocoTestReport {
    reports {
        xml.enabled(true)
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                // minimum = 0.90
            }
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                // minimum = 0.80
            }
            limit {
                counter = 'LINE'
                value = 'TOTALCOUNT'
                // maximum = 200
            }
            excludes = [
                    // "com.example.somewhere.*"
            ]
        }
    }
}

publishing {
    // Declare all publications
    publications {
        mavenJava(MavenPublication) { from components.java }
    }

    // Add repositories to publish to
    repositories {
        // GitHub Packages (https://pkg.github.com)
        maven {
            name 'GitHub'
            url "https://maven.pkg.github.com/${project.github_repo}"
            credentials {
                username System.getenv('GITHUB_ACTOR')
                password System.getenv('GITHUB_TOKEN')
            }
        }
    }
}

bootBuildImage {
    builder = "paketobuildpacks/builder:tiny"
    environment = [
            "BP_NATIVE_IMAGE": "true"
    ]
}

jooq {
    configurations {
        main {  // name of the jOOQ configuration
            generateSchemaSourceOnCompilation = false  // default (can be omitted)
            generationTool {
                jdbc {
                    driver = 'org.postgresql.Driver'
                    url = "jdbc:postgresql://${System.getenv("API_DB_HOST")}/${System.getenv("API_DB_DATABASE") == null ? "discogs" : System.getenv("API_DB_DATABASE")}"
                    user = "${System.getenv("API_DB_USERNAME")}"
                    password = "${System.getenv("API_DB_PASSWORD")}"
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        inputSchema = 'public'
                        forcedTypes {
                            forcedType {
                                name = 'varchar'
                                includeExpression = '.*'
                                includeTypes = 'JSONB?'
                            }
                            forcedType {
                                name = 'varchar'
                                includeExpression = '.*'
                                includeTypes = 'INET'
                            }
                        }
                    }
                    generate {
                        deprecated = false
                        records = true
                        immutablePojos = true
                        fluentSetters = true
                    }
                    target {
                        packageName = 'io.dsub.opendiscogs.jooq'
                        directory = 'src/main/generated/java'  // default (can be omitted)
                    }
                    strategy.name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                }
            }
        }
    }
}